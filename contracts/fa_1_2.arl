archetype fa1_2

constant totalsupply : nat = 10_000_000

asset ledger identified by holder to big_map {
  holder     : address;
  tokens     : nat = 0;
  allowance  : map<address, nat> = [];
} initialized by {
  { holder = caller; tokens = totalsupply }
}

entry %transfer (%from : address, %to : address, value : nat) {
  require {
    r1 : ledger[%from].tokens >= value otherwise "NotEnoughBalance";
  }
  effect {
    if caller <> %from then (
      const current = ledger[%from].allowance[caller] ? the : 0;
      const new_value ?= int_to_nat(current - value) : ("NotEnoughAllowance", ((value, current)));
      ledger[%from].allowance.put(caller, new_value);
    );
    ledger.update(%from, { tokens -= value });
    ledger.add_update(%to, { tokens += value });
  }
}

entry approve(spender : address, value : nat) {
  var k = (caller, spender);
  const previous = ledger[caller] ? (the.allowance[spender] ? the : 0) : 0;
  do_fail_if(previous > 0 and value > 0, (("UnsafeAllowanceChange", previous)));
  ledger[caller].allowance.put(spender, value);
}

getter getAllowance (owner : address, spender : address) : nat {
  return (ledger[owner] ? (the.allowance[spender] ? the : 0) : 0)
}

getter getBalance (owner : address) : nat {
  return (ledger[owner] ? the.tokens : 0)
}

getter getTotalSupply () : nat {
  return totalsupply
}